desc(
    title: "Find Java File Read Sinks",
    title_zh: "查找 Java 文件读取接收点",
    type: audit,
    lib: "read-filename-sink"
)

// FileReader reader = new FileReader("example.txt");
FileReader(*?{!have: FileReader} as $fileReader);

// BufferedReader reader = new BufferedReader(new FileReader("example.txt"));
BufferedReader(*?{!have: BufferedReader} as $bufferedReader);

// FileInputStream inputStream = new FileInputStream("example.txt");
FileInputStream(*?{!have: FileInputStream} as $fileInputStream);

// java.nio.file.Files.readAllBytes,java.nio.file.Files.readString etc...
Files?{<typeName>?{have: 'java.nio'}}.read*(* as $nioFileRead);

// java.nio.file.Files.newBufferedReader
Files?{<typeName>?{have: 'java.nio'}}.newBufferedReader(* as $nioBufferedReader);

// java.nio.file.Files.newInputStream
Files?{<typeName>?{have: 'java.nio'}}.newInputStream(* as $nioInputStream);


$fileReader as $output;
$bufferedReader as $output;
$fileInputStream as $output;
$nioFileRead as $output;
$nioBufferedReader as $output;
$nioInputStream as $output;
alert $output;
