desc(
	title: 'checking [Servlet Web Parameters Finding]'
	type: audit
	lib: 'java-servlet-param'
	desc: <<<TEXT
此规则旨在审计Java Servlet应用程序中可能存在的Web参数安全问题。通过检测`HttpServletRequest`对象中的参数获取方法（如`getParameter`和通过`getInputStream`读取请求体），此规则能够识别出应用程序可能未充分验证或清理用户输入的数据。未验证或未清理的用户输入可能导致各种安全漏洞，包括但不限于SQL注入、跨站脚本（XSS）攻击和跨站请求伪造（CSRF）攻击。因此，建议开发者严格验证和清理所有用户输入，以确保应用程序的安全性。
TEXT
	rule_id: "a258e671-339e-4d8f-af91-e2493a4e91dd"
)

HttpServletRequest?{opcode:param}?{<typeName>?{have:'javax.servlet.http.HttpServletRequest'}} as $req
/(do(Get|Post|Delete|Filter|[A-Z]\w+))|(service)/<getFormalParams>?{!have: this && opcode: param } as $req;
$req.getParameter() as $directParam;
$req -{
  hook: `*.getParameter() as $indirectParam`
}->;
.getInputStream?{<getObject><typeName>?{have: *Http*Request*}}() as $getInputStream;
.getSession?{<getObject><typeName>?{have: *Http*Request*}}() as $getSession;
$directParam + $indirectParam + $getInputStream +$getSession as $output;
alert $output;

desc(
	lang: java
	alert_num:1
	'file://demo.java':<<<EOF
import javax.servlet.http.HttpServletRequest;

class Demo{
   public void trace_route(HttpServletRequest request, HttpServletResponse response) throws IOException {
         String ip = request.getParameter("ip");
   }
}
EOF
)
