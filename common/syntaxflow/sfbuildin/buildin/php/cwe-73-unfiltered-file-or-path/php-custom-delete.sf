desc(
	title: "PHP Unfiltered File or Path Operation Detection"
	title_zh: "PHP未过滤文件或路径操作检测",
	risk: "audit"
	level: "low"
	desc: <<<CODE
该规则集旨在检测PHP代码中可能存在的未过滤文件或路径操作，这些操作可能导致安全风险，如文件包含漏洞、目录遍历等。规则通过静态分析代码中的文件操作函数（如opendir）及其参数来源，来识别潜在的安全问题。
CODE
	rule_id: "a6f3be4b-5f16-46d8-8e0b-9182e5078345"
)
<include('php-param')> as $params
<include('php-tp-all-extern-variable-param-source')> as $params
<include('php-filter-function')> as $filter

opendir(* as $allSink)

$allSink?{<self> #{include: <<<CODE
* & $params
CODE}->} as $sink

$sink<dataflow(include=<<<CODE
* & $params as $__next__
CODE,exclude=<<<CODE
*?{opcode: call} as $__next__
CODE)> as $high

alert $high for {
    title: "Unsafe File Operation Without Validation",
    title_zh: "未经验证的文件操作",
    level: "high"
}
$sink<dataflow(include=<<<CODE
* & $params as $__next__
CODE,exclude=<<<CODE
*?{opcode:call && <self><getCallee> & $filter} as $__next__
CODE)> as $highAndMid

$highAndMid - $high as $mid
alert $mid for {
    title: "Insecure File Type Detection",
    title_zh: "不安全的文件类型检测",
    level: "mid"
}
$all - $high - $mid as $low
alert $low for {
    title: "Potential File Storage Path Misconfiguration",
    title_zh: "潜在的文件存储路径配置问题",
    level: "low"
}

desc(
	lang: "php"
	alert_exact: 1
	'file:///unsafe.php': <<<CODE
<?php
    $a = $_GET[1];
    opendir($a);
CODE
)
