desc(
	title: " Open Redirect Vulnerability"
	title_zh: "重定向漏洞",
	level: "low"
	risk: "open-redirect"
	type: "audit"
	desc: <<<TEXT
开放重定向漏洞（CWE-601）允许攻击者构造恶意URL诱导用户访问钓鱼网站。当应用程序未对header()函数中的重定向目标进行严格验证时，攻击者可通过篡改URL参数（如redirect_to）将用户导向恶意域名。
TEXT
	rule_id: "65934f87-f276-44cc-826a-3480b5a57ff5"
)


<include('php-param')> as $params
<include('php-tp-all-extern-variable-param-source')> as $params
<include('php-filter-function')> as $filter

header(* as $sink)

$sink #{
    include: `<self> & $params`
    }-> as $all

$all<dataflow(exclude=<<<CODE
<self>?{opcode: call} as $__next__
CODE)> as $high

alert $high for{
   title: "Unvalidated Redirect Vulnerability",
   title_zh: "检测到用户输入($source)直接传递给header()函数且未经过任何过滤函数调用",
   type: "vuln",
   level: "high"
}

$all<dataflow(exclude=<<<CODE
<self>?{opcode: call && <self><getCallee> & $filter} as $__next__
CODE)> as $highAndMid

$highAndMid - $high as $mid
alert $mid for{
   title: "Insufficient Redirect Validation",
   title_zh: "检测到存在过滤函数($filter)但未完全验证目标域名的合法性",
   type: "mid",
   level: "mid"
}

$all - $high - $mid as $low

alert $low for{
   title: "Potential Redirect Validation Bypass",
   title_zh: "检测到已使用过滤函数但存在验证逻辑缺陷的潜在绕过风险",
   type: "low",
   level: "low"
}

desc(
	lang: "php"
	alert_min: 1
	alert_high: 1
	'file:///unsafe.php': <<<TEXT
<?php
    $url = $_GET["url"];
    header("Location: ".$url);
TEXT
)
