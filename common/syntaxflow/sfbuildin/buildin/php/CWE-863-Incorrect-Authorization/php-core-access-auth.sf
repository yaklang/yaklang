desc(
	title: "Insecure File Operation"
	title_zh: "未经验证的文件操作风险",
	type: vul
	risk: "file-operation"
	desc: <<<TEXT
        检测未经验证的用户输入直接用于文件操作系统函数,可能导致任意文件读取、写入、删除等风险,建议对用户输入进行严格过滤或验证,避免直接传递给文件操作系统函数.
TEXT
	rule_id: "7e2c4412-9a6f-4095-a84f-beb680e56fbf"
)
/copy|(sym)?link|rename|fopen|fread|file_get_contents/ as $function

<include('php-param')> as $params;
<include('php-tp-all-extern-variable-param-source')> as $params

$function(* as $allParams)

$allParams?{<self> #{include: <<<CODE
* & $params
CODE}->} as $sink

$sink<dataflow(include=<<<CODE
* & $params as $__next__
CODE,exclude=<<<CODE
*?{opcode: call} as $__next__
CODE)> as $high

alert $high for {
   title: "Direct File Operation Without Validation",
   title_zh: "未经验证的文件操作高危漏洞",
   level: "high",
}

$sink<dataflow(include=<<<CODE
* & $params as $__next__
CODE,exclude=<<<CODE
*?{opcode: call && <self><getCallee> & $filter} as $__next__
CODE)> as $mid

alert $mid for {
   title: "Insufficient Parameter Filtering",
   title_zh: "参数过滤不完整风险",
   level: "mid",
}

$sink - $high - $mid  as $low

alert $low for {
   title: "Potential Access Control Bypass",
   title_zh: "潜在访问控制绕过风险",
   level: "low",
}

desc(
	lang: "php"
	alert_min: 1
	"file:///unsafe.php": <<<TEXT
<?php
    $origin = $_GET["origin"];
    $destination = $_GET["destination"];
    copy($origin,$destination);
TEXT
)
