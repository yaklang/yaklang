yakit.AutoInitYakit()

# Input your code!

query := cli.String("query", cli.setVerboseName("查询问题"), cli.setRequired(true))

enhancePlan := cli.StringSlice(
    "enhancePlan",
    cli.setVerboseName("增强搜索方案"),
    cli.setSelectOption("假设回答", "hypothetical_answer"),
    cli.setSelectOption("假设并多次查询", "hypothetical_answer_with_split"),
    cli.setSelectOption("多次查询", "split_query"),
    cli.setSelectOption("泛化回答", "generalize_query"),
)
kbName := cli.String("kbName", cli.setVerboseName("知识库名"))
limit := cli.Int("limit", cli.setVerboseName("结果数量限制"), cli.setDefault(20))
concurrent := cli.Int("concurrent", cli.setVerboseName("并发数"), cli.setDefault(20))
score := cli.Float("score", cli.setVerboseName("结果分数限制"))
cli.check()

option := [rag.queryCtx(context.Background()), rag.queryLimit(limit), rag.queryConcurrent(concurrent), rag.queryStatus(yakit.StatusCard)]

if len(enhancePlan) > 0 {
    option = append(option, rag.queryEnhance(enhancePlan[0]))
}


if kbName != "" {
    option = append(option, rag.queryCollection(kbName))
}


if score > 0 {
    option = append(option, rag.queryScoreLimit(score))
}


tablename := "检索结果"
columnType := "类型"
columnContent := "内容"
columnScore := "分数"
columnQueryMethod := "查询方式"
columnSource := "来源"
columnTime := "时间"
columnQuery := "查询语句"

yakit.EnableTable(
    tablename,
    [columnContent, columnQueryMethod, columnQuery, columnType, columnScore, columnSource, columnTime],
)

addData = func(typeName, data, score, source, timeString, queryMethod, query) {
    yakit.TableData(
        tablename,
        {columnType: typeName, columnContent: data, columnScore: score, columnSource: source, columnTime: timeString, columnQueryMethod: queryMethod, columnQuery: query},
    )
}

for i in rag.Query(query, option...)~ {
    if i.Type == "message" {
        yakit.Info(i.Message)
        continue
    }

    if i.Type != "mid_result" {
        addData(
            i.Data.Type,
            string(i.Data.Content),
            "%.4f" % i.Score,
            i.Source,
            timestampToDatetime(i.Timestamp / 1000),
            i.QueryMethod,
            i.QueryOrigin,
        )
    }
}