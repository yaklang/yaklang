yakit.AutoInitYakit()

configJSON := cli.Text("config",
    cli.setRequired(true),
    cli.setVerboseName("SSA 配置"),
    cli.setHelp("SSA 项目的配置 JSON 字符串")
)

cli.check()

yakit.Info("开始编译 SSA 项目")

config, err = ssa.NewConfig(ssa.ModeAll, ssa.withJsonRawConfig(configJSON))
if err != nil {
    yakit.Error("创建配置失败: %v", err)
    return
}

if config == nil {
    yakit.Error("配置为空")
    return
}

projectName = config.GetProjectName()
yakit.Info("项目名称: %s", projectName)
yakit.Info("项目语言: %s", config.GetLanguage())

currentTime = time.Now().Format("2006-01-02 15:04:05")
programName = sprintf("%s(%s)", projectName, currentTime)
yakit.Info("程序名称: %s", programName)

opts = [
    ssa.withProcess((msg, process) => {
        yakit.SetProgressEx("compile", process)
        yakit.Info("%s: %f", msg, process)
    }),
    ssa.withContext(context.Background()),
    ssa.withJsonRawConfig(configJSON),
    ssa.withProgramName(programName),
]

progs, err = ssa.ParseProject(opts...)
if err != nil {
    yakit.Text("编译错误信息:\n" + err.Error())
    yakit.Error("编译失败")
    return
}

yakit.Info("编译完成")

yakit.Code(json.dumps({"program_name": programName}))

