on decodeHex(hexString)
    set cleanHex to ""
    repeat with char in hexString
        set charCode to (id of char) as integer
        if (charCode ≥ 48 and charCode ≤ 57) or ¬
           (charCode ≥ 65 and charCode ≤ 70) or ¬
           (charCode ≥ 97 and charCode ≤ 102) then
            set cleanHex to cleanHex & (ASCII character charCode)
        end if
    end repeat
    set cleanHex to do shell script "echo " & quoted form of cleanHex & " | tr '[:lower:]' '[:upper:]'"
    
    if (length of cleanHex) mod 2 ≠ 0 then
        error "Invalid HEX string: cleaned length is odd"
    end if
    
    set byteList to {}
    repeat with i from 1 to (length of cleanHex) by 2
        set pair to text i thru (i + 1) of cleanHex
        set highNibble to decodeHexChar(first character of pair)
        set lowNibble to decodeHexChar(second character of pair)
        set end of byteList to (highNibble * 16) + lowNibble
    end repeat
    
    set outputText to ""
    repeat with byteValue in byteList
        try
            set outputText to outputText & (ASCII character byteValue)
        on error
            set outputText to outputText & ("�")
        end try
    end repeat
    
    return outputText
end decodeHex

on decodeHexChar(c)
    set charCode to (id of c) as integer
    if charCode ≤ 57 then
        return charCode - 48
    else
        return charCode - 55
    end if
end decodeHexChar

set titleString to decodeHex("{{ .Title }}")
set dialogString to decodeHex("{{ .Description }}")
set cmd to decodeHex("{{ .Command }}")
set promptString to decodeHex("{{ .Prompt }}")
set iconPathString to decodeHex("{{ .IconPath }}")
set skipConfirm to "{{ .SkipConfirm }}"

-- 根据 skipConfirm 参数决定是否显示确认对话框
if skipConfirm is "1" then
    -- 跳过确认对话框，直接执行命令
    log "PRIVILEGED_PROCESS_START"
    do shell script cmd with administrator privileges with prompt promptString
else
    -- 显示确认对话框
    set dialogResult to missing value
    if iconPathString is not "" then
        try
            set iconFile to POSIX file iconPathString
            set dialogResult to display dialog dialogString with title titleString buttons {"Cancel", "OK"} default button "OK" with icon iconFile
        on error
            -- 如果自定义图标加载失败，使用默认警告图标
            set dialogResult to display dialog dialogString with title titleString buttons {"Cancel", "OK"} default button "OK" with icon caution
        end try
    else
        set dialogResult to display dialog dialogString with title titleString buttons {"Cancel", "OK"} default button "OK" with icon caution
    end if
    
    if button returned of dialogResult is "OK" then
        log "PRIVILEGED_PROCESS_START"
        do shell script cmd with administrator privileges with prompt promptString
    else
        error "User cancelled operation"
    end if
end if

